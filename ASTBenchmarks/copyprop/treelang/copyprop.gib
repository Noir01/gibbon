#lang gibbon

(require "../../grammar_racket.gib" (only-in racket gensym))

(provide copyprop)

(define (copyprop [e : Toplvl]) : Toplvl
  (let ([nexpr : Toplvl (top-pass1 e)])
    (let ([mut : (SymDict Bool) (top-pass2 nexpr (ann (empty-dict) (SymDict Bool)))])
      (top-pass3 nexpr mut))))

(define (loop1 [ls : ListToplvl]) : ListToplvl
  (case ls
    [(CONSTOPLVL tl ls0)
     (CONSTOPLVL (top-pass1 tl) (loop1 ls0))]
    [(NULLTOPLVL)
     ls]))

(define (loop-begintop [ls : ListToplvl] [mut : (SymDict Bool)]) : (SymDict Bool)
  (case ls
    [(CONSTOPLVL tl ls0)
     (loop-begintop ls0 (top-pass2 tl mut))]
    [(NULLTOPLVL)
     mut]))

(define (loop-begintop2 [ls : ListToplvl] [mut : (SymDict Bool)]) : ListToplvl
  (case ls
    [(CONSTOPLVL tl ls0)
     (CONSTOPLVL (top-pass3 tl mut) (loop-begintop2 ls0 mut))]
    [(NULLTOPLVL)
     ls]))

;; call pass1 on list of exprs
(define (loop2 [ls : ListExpr] [env : (SymDict Sym)]) : ListExpr
  (case ls
    [(CONSEXPR e ls0)
     (CONSEXPR (pass1 e env) (loop2 ls0 env))]
    [(NULLEXPR)
     ls]))

(define (listexpr-pass3 [ls : ListExpr] [env : (SymDict Sym)] [mut : (SymDict Bool)]) : ListExpr
  (case ls
    [(CONSEXPR e ls0)
     (CONSEXPR (pass3 e env mut)
               (listexpr-pass3 ls0 env mut))]
    [(NULLEXPR)
     ls]))

(define (listexpr-pass2 [ls : ListExpr] [mut : (SymDict Bool)]) : (SymDict Bool)
  (case ls
    [(CONSEXPR e ls0)
     (listexpr-pass2 ls0 (pass2 e mut))]
    [(NULLEXPR)
     mut]))

(define (caselambda-pass2 [ls : LAMBDACASE] [mut : (SymDict Bool)]) : (SymDict Bool)
  (case ls
    [(CONSLAMBDACASE fs le ls0)
     (caselambda-pass2 ls0 (listexpr-pass2 le mut))]
    [(NULLLAMBDACASE)
     mut]))

(define (caselambda-pass3 [ls : LAMBDACASE] [env : (SymDict Sym)] [mut : (SymDict Bool)]) : LAMBDACASE
  (case ls
    [(CONSLAMBDACASE fs le ls0)
     (CONSLAMBDACASE fs (listexpr-pass3 le env mut)
                     (caselambda-pass3 ls0 env mut))]
    [(NULLLAMBDACASE)
     ls]))

;; rename each lambda's params
;; update params with new names
;; update body
;; loop
(define (loop3 [ls : LAMBDACASE] [env : (SymDict Sym)]) : LAMBDACASE
  (case ls
    [(CONSLAMBDACASE fs le ls0)
     (let ([nenv : (SymDict Sym) (formals-ee fs env)])
       (CONSLAMBDACASE (formals-update fs nenv)
                       (loop2 le nenv)
                       (loop3 ls0 env)))]
    [(NULLLAMBDACASE)
     ls]))

;; updates
(define (lookup-syms [syms : ListSym] [env : (SymDict Sym)]) : ListSym
  (case syms
    [(CONSSYM s rest)
     (CONSSYM (if (has-key? (ann env Sym) s)
                  (lookup (ann env Sym) s)
                  s)
              (lookup-syms rest env))]
    [(NULLSYM)
     syms]))

(define (loop6 [ls : ListSym] [env : (SymDict Sym)]) : (SymDict Sym)
  (case ls
    [(CONSSYM s ls0)
     (loop6 ls0 (insert env s (ann (gensym) Sym)))]
    [(NULLSYM)
     env]))

(define (loop7 [ls : ListSym] [env : (SymDict Sym)]) : ListSym
  (case ls
    [(CONSSYM s ls0)
     (CONSSYM (lookup (ann env Sym) s) (loop7 ls0 env))]
    [(NULLSYM)
     ls]))

(define (top-pass1 [e : Toplvl]) : Toplvl
  (case e
    [(DefineValues ls e0)
     (DefineValues ls (pass1 e0 (ann (empty-dict) (SymDict Sym))))]
    [(DefineSyntaxes ls e0)
     (DefineSyntaxes ls (pass1 e0 (ann (empty-dict) (SymDict Sym))))]
    [(BeginTop ls)
     (BeginTop (loop1 ls))]
    [(Expression e0)
     (Expression (pass1 e0 (ann (empty-dict) (SymDict Sym))))]))

(define (top-pass2 [e : Toplvl] [mut : (SymDict Bool)]) : (SymDict Bool)
  (case e
    [(DefineValues ls e0)
     (pass2 e0 mut)]
    [(DefineSyntaxes ls e0)
     (pass2 e0 mut)]
    [(BeginTop ls)
     (loop-begintop ls mut)]
    [(Expression e0)
     (pass2 e0 mut)]))

(define (top-pass3 [e : Toplvl] [mut : (SymDict Bool)]) : Toplvl
  (case e
    [(DefineValues ls e0)
     (DefineValues ls (pass3 e0 (ann (empty-dict) (SymDict Sym)) mut))]
    [(DefineSyntaxes ls e0)
     (DefineSyntaxes ls (pass3 e0 (ann (empty-dict) (SymDict Sym)) mut))]
    [(BeginTop ls)
     (BeginTop (loop-begintop2 ls mut))]
    [(Expression e0)
     (Expression (pass3 e0 (ann (empty-dict) (SymDict Sym)) mut))]))

;; rename variables
(define (pass1 [e : Expr] [env : (SymDict Sym)]) : Expr
  (case e
    [(VARREF s)
     (VARREF (if (has-key? (ann env Sym) s)
                 (lookup (ann env Sym) s)
                 s))]
    [(Top s)
     e]
    [(VariableReference s)   ; #%variable-reference
     e]
    [(VariableReferenceTop s)   ; #%variable-reference (#%top . id)
     e]
    ;; Leaf forms:
    [(VariableReferenceNull)     ; (#%variable-reference)
     e]
    [(Quote d) e]
    [(QuoteSyntax d) e]
    [(QuoteSyntaxLocal d) e] ;; (quote-syntax datum #:local)

    ;; Binding forms:
    [(Lambda formals body)
     (let ([nenv : (SymDict Sym) (formals-ee formals env)]) ;; rename the params
       (Lambda (formals-update formals nenv) ;; update params with new names
               (loop2 body nenv)))] ;; update body with new names

    [(CaseLambda cases)
     (CaseLambda (loop3 cases env))] ;; do the above, but for case lambda

    [(LetValues binds body)
     (let ([nenv : (SymDict Sym) (lvbind-rename binds env)])
       (let ([nbinds : LVBIND (lvbind-update-lhs binds nenv)])
         (let ([nbinds : LVBIND (lvbind-update-rhs nbinds env)]) ;; env because not a let*
           (LetValues nbinds (loop2 body nenv)))))]

    [(LetrecValues binds body) ;; anything different here? one thing
     (let ([nenv : (SymDict Sym) (lvbind-rename binds env)])
       (let ([nbinds : LVBIND (lvbind-update-lhs binds nenv)])
         (let ([nbinds : LVBIND (lvbind-update-rhs nbinds nenv)]) ;; nenv because recursive.
           (LetrecValues nbinds (loop2 body nenv)))))]

    [(If cond then else)
     (If (pass1 cond env) (pass1 then env) (pass1 else env))]
    [(Begin exprs)
     (Begin (loop2 exprs env))]
    [(Begin0 e1 exprs)
     (Begin0 (pass1 e1 env) (loop2 exprs env))]
    [(App e1 exprs)  ;; (#%plain-app expr ...+)
     (App (pass1 e1 env) (loop2 exprs env))]
    [(SetBang s e0)
     (SetBang (if (has-key? (ann env Sym) s)
                  (lookup (ann env Sym) s)
                  s)
              (pass1 e0 env))]
    [(WithContinuationMark e1 e2 e3)
     (WithContinuationMark (pass1 e1 env) (pass1 e2 env) (pass1 e3 env))]))

;; gather list of mutated variables
(define (pass2 [e : Expr] [mut : (SymDict Bool)]) : (SymDict Bool)
  (case e
    [(VARREF s)
     mut]
    [(Top s)
     mut]
    [(VariableReference s)   ; #%variable-reference
     mut]
    [(VariableReferenceTop s)   ; #%variable-reference (#%top . id)
     mut]
    ;; Leaf forms:
    [(VariableReferenceNull)     ; (#%variable-reference)
     mut]
    [(Quote d) mut]
    [(QuoteSyntax d) mut]
    [(QuoteSyntaxLocal d) mut] ;; (quote-syntax datum #:local)

    ;; Binding forms:
    [(Lambda formals body)
     (listexpr-pass2 body mut)]

    [(CaseLambda cases)
     (caselambda-pass2 cases mut)]

    [(LetValues binds body)
     ;; need to look for setbang on rhs
     (listexpr-pass2 body (lvbind-pass2 binds mut))]

    [(LetrecValues binds body)
     (listexpr-pass2 body (lvbind-pass2 binds mut))]

    [(If cond then else)
     (let ([mut : (SymDict Bool) (pass2 cond mut)])
       (let ([mut : (SymDict Bool) (pass2 then mut)])
         (pass2 else mut)))]

    [(Begin exprs)
     (listexpr-pass2 exprs mut)]

    [(Begin0 e1 exprs)
     (listexpr-pass2 exprs (pass2 e1 mut))]

    [(App e1 exprs)  ;; (#%plain-app expr ...+)
     (listexpr-pass2 exprs (pass2 e1 mut))]

    [(SetBang s e0)
     (insert mut s (ann True Bool))]

    [(WithContinuationMark e1 e2 e3)
     (let ([mut : (SymDict Bool) (pass2 e1 mut)])
       (let ([mut : (SymDict Bool) (pass2 e2 mut)])
         (pass2 e3 mut)))]))

(define (pass3 [e : Expr] [env : (SymDict Sym)] [mut : (SymDict Bool)]) : Expr
  (case e
    [(VARREF s)
     (VARREF (if (has-key? (ann mut Bool) s)
                 s
                 (if (has-key? (ann env Sym) s)
                     (lookup (ann env Sym) s)
                     s)))]
    [(Top s)
     e]
    [(VariableReference s)   ; #%variable-reference
     e]
    [(VariableReferenceTop s)   ; #%variable-reference (#%top . id)
     e]
    ;; Leaf forms:
    [(VariableReferenceNull)     ; (#%variable-reference)
     e]
    [(Quote d) e]
    [(QuoteSyntax d) e]
    [(QuoteSyntaxLocal d) e] ;; (quote-syntax datum #:local)

    ;; Binding forms:
    [(Lambda formals body)
     (Lambda formals (listexpr-pass3 body env mut))]

    [(CaseLambda cases)
     (CaseLambda (caselambda-pass3 cases env mut))]

    [(LetValues binds body)
     ;; extend environment.
     (let ([nenv : (SymDict Sym) (letvalues-extend-env binds env)])
       (LetValues binds (listexpr-pass3 body nenv mut)))]

    [(LetrecValues binds body)
     (let ([lhs : (SymDict Bool) (list-of-syms binds)])
       (let ([nenv : (SymDict Sym) (letrecvalues-extend-env binds lhs env)])
         (LetrecValues binds (listexpr-pass3 body nenv mut))))]

    [(If cond then else)
     (If (pass3 cond env mut)
         (pass3 then env mut)
         (pass3 else env mut))]

    [(Begin exprs)
     (Begin (listexpr-pass3 exprs env mut))]

    [(Begin0 e1 exprs)
     (Begin0 (pass3 e1 env mut) (listexpr-pass3 exprs env mut))]

    [(App e1 exprs)  ;; (#%plain-app expr ...+)
     (App (pass3 e1 env mut) (listexpr-pass3 exprs env mut))]

    [(SetBang s e0)
     (SetBang s (pass3 e0 env mut))]

    [(WithContinuationMark e1 e2 e3)
     (WithContinuationMark (pass3 e1 env mut)
                           (pass3 e2 env mut)
                           (pass3 e3 env mut))]))

(define (list-of-syms [lv : LVBIND]) : (SymDict Bool)
  (case lv
    [(CONSLVBIND syms e lv0)
     (insert-syms syms (list-of-syms lv0))]
    [(NULLLVBIND)
     (ann (empty-dict) (SymDict Bool))]))

(define (insert-syms [ls : ListSym] [syms : (SymDict Bool)]) : (SymDict Bool)
  (case ls
    [(CONSSYM s ls0)
     (insert-syms ls0 (insert syms s (ann True Bool)))]
    [(NULLSYM)
     syms]))

(define (letrecvalues-extend-env [lv : LVBIND] [lhs : (SymDict Bool)] [env : (SymDict Sym)]) : (SymDict Sym)
  (case lv
    [(CONSLVBIND syms e lv0)
     (letrecvalues-extend-env lv0 lhs (case e
                                        [(VARREF s)
                                         (if (has-key? (ann env Sym) s)
                                             env
                                             (listsym-extend-env syms s env))]
                                        [(Lambda formals list-expr) env]
                                        [(CaseLambda lambda-case) env]
                                        [(If test conseq alt) env]
                                        [(Begin list-expr) env]
                                        [(Begin0 expr list-expr) env]
                                        [(LetValues lvbind list-expr) env]
                                        [(LetrecValues lvbind list-expr) env]
                                        [(SetBang sym expr) env]
                                        [(Quote datum) env]
                                        [(QuoteSyntax datum) env]
                                        [(QuoteSyntaxLocal datum) env]
                                        [(WithContinuationMark e1 e2 e3) env]
                                        [(App expr list-expr) env]
                                        [(Top sym) env]
                                        [(VariableReference sym) env]
                                        [(VariableReferenceTop sym) env]
                                        [(VariableReferenceNull) env]))]
    [(NULLLVBIND)
     env]))

(define (letvalues-extend-env [lv : LVBIND] [env : (SymDict Sym)]) : (SymDict Sym)
  (case lv
    [(CONSLVBIND syms e lv0)
     (letvalues-extend-env lv0 (case e
                                 [(VARREF s)
                                  (listsym-extend-env syms s env)]
                                 [(Lambda formals list-expr) env]
                                 [(CaseLambda lambda-case) env]
                                 [(If test conseq alt) env]
                                 [(Begin list-expr) env]
                                 [(Begin0 expr list-expr) env]
                                 [(LetValues lvbind list-expr) env]
                                 [(LetrecValues lvbind list-expr) env]
                                 [(SetBang sym expr) env]
                                 [(Quote datum) env]
                                 [(QuoteSyntax datum) env]
                                 [(QuoteSyntaxLocal datum) env]
                                 [(WithContinuationMark e1 e2 e3) env]
                                 [(App expr list-expr) env]
                                 [(Top sym) env]
                                 [(VariableReference sym) env]
                                 [(VariableReferenceTop sym) env]
                                 [(VariableReferenceNull) env]))]
    [(NULLLVBIND)
     env]))

(define (listsym-extend-env [ls : ListSym] [sym : Sym] [env : (SymDict Sym)]) : (SymDict Sym)
  (case ls
    [(CONSSYM s ls0)
     (listsym-extend-env ls0 sym (insert env s (ann sym Sym)))]
    [(NULLSYM)
     env]))

;; gensym formals and extend the env with the mapping
(define (formals-ee [f : Formals] [env : (SymDict Sym)]) : (SymDict Sym)
  (case f
    [(F1 ls)
     (loop6 ls env)]
    [(F2 ls s)
     (let ([nenv : (SymDict Sym) (loop6 ls env)])
       (insert nenv s (ann (gensym) Sym)))]
    [(F3 s)
     (insert env s (ann (gensym) Sym))]))

;; update formals to match new name in env
(define (formals-update [f : Formals] [env : (SymDict Sym)]) : Formals
  (case f
    [(F1 ls)
     (F1 (loop7 ls env))]
    [(F2 ls s)
     (F2 (loop7 ls env) (lookup (ann env Sym) s))]
    [(F3 s)
     (F3 (lookup (ann env Sym) s))]))

;; gensym for a list of syms and insert mappings into environment
(define (rename-syms [ls : ListSym] [env : (SymDict Sym)]) : (SymDict Sym)
  (case ls
    [(CONSSYM s ls0)
     (rename-syms ls0 (insert env s (ann (gensym) Sym)))]
    [(NULLSYM)
     env]))

(define (lvbind-pass2 [ls : LVBIND] [mut : (SymDict Bool)]) : (SymDict Bool)
  (case ls
    [(CONSLVBIND syms e ls0)
     (lvbind-pass2 ls0 (pass2 e mut))]
    [(NULLLVBIND)
     mut]))

;; gensym let bindings and extend env with new mappings.
(define (lvbind-rename [ls : LVBIND] [env : (SymDict Sym)]) : (SymDict Sym)
  (case ls
    [(CONSLVBIND syms e ls0)
     (lvbind-rename ls0 (rename-syms syms env))]
    [(NULLLVBIND)
     env]))

;;
(define (lvbind-update-lhs [ls : LVBIND] [env : (SymDict Sym)]) : LVBIND
  (case ls
    [(CONSLVBIND syms e ls0)
     (CONSLVBIND (lookup-syms syms env)
                 e ;; what about here?
                 (lvbind-update-lhs ls0 env))]
    [(NULLLVBIND)
     ls]))

(define (lvbind-update-rhs [ls : LVBIND] [env : (SymDict Sym)]) : LVBIND
  (case ls
    [(CONSLVBIND syms e ls0)
     (CONSLVBIND syms (pass1 e env) (lvbind-update-rhs ls0 env))]
    [(NULLLVBIND)
     ls]))
